#!/bin/sh  
# \
exec oagwish "$0" "$@"

set auto_path [linsert $auto_path 0  /usr/local/oag/apps/lib/$env(HOST_ARCH)]
set auto_path [linsert $auto_path 0 /usr/local/oag/lib_patch/$env(HOST_ARCH)]
APSStandardSetup

set usage "usage: longitCalcs -twiss <filename>"
set twiss ""
set args $argv
if {[APSStrictParseArguments {twiss}] || ![string length $twiss]} {
    puts stderr $usage
    exit 1
}
if ![file exists $twiss] {
    puts stderr "Not found: $twiss"
    exit 1
}

set dataItemList [list U0 Sdelta0 ex0 alphac pCentral revFreq]
if [catch {exec sddsconvert $twiss -pipe=out -topage=1 \
             | sddsprocess -pipe -process=s,max,sMax \
             "-define=parameter,revFreq,sMax pCentral beta.p c_mks * / rec" \
             | sdds2stream -pipe \
             -parameter=[join $dataItemList ,]} dataList] {
    puts stderr "$dataList"
    exit 1
}
set index 0
foreach item $dataItemList {
    set $item [lindex $dataList $index]
    incr index
}

APSApplication . -name longitCalcs 
set voltage 9
set harmonic 1296
APSLabeledEntry .voltage -parent .userFrame \
  -label "Voltage (MV): " -textVariable voltage -width 22
bind .userFrame.voltage.entry <Return> compute
APSLabeledEntry .harmonic -parent .userFrame \
  -label "Harmonic number: " -textVariable harmonic -width 22
bind .userFrame.harmonic.entry <Return> compute

set synchTune 0
set bunchLength 0
set synchPhaseDeg 0
set overVoltage 0
set rfAcceptance 0
set rfFrequency 0
APSLabeledOutput .rfFreq -parent .userFrame \
    -label "RF frequency (MHz): " -textVariable rfFrequency -width 22
APSLabeledOutput .synchphase -parent .userFrame \
  -label "Synchrotron Phase: " -textVariable synchPhaseDeg -width 22
APSLabeledOutput .overvolt -parent .userFrame \
    -label "Over Voltage: " -textVariable overVoltage  -width 22
APSLabeledOutput .accept -parent .userFrame \
    -label "RF Acceptance: " -textVariable rfAcceptance -width 22
APSLabeledOutput .nus -parent .userFrame \
  -label "Synchrotron Tune: " -textVariable synchTune  -width 22
APSLabeledOutput .blen -parent .userFrame \
  -label "Bunch length (m): " -textVariable bunchLength -width 22

proc compute {} {
    global dataItemList
    eval global $dataItemList synchTune bunchLength harmonic voltage synchPhaseDeg
    global overVoltage rfAcceptance rfFrequency
    set pi 3.141592653589793
    set cMKS 2.997924580000000e+08
    set energy [expr sqrt($pCentral*$pCentral+1)*0.51099906]
    set synchPhase [expr asin($U0/$voltage)]
    set overVoltage [expr $voltage/$U0]
    set rfAcceptance [expr sqrt(2*$U0/($pi*$alphac*$harmonic*$energy)*(sqrt($overVoltage*$overVoltage-1)-acos(1/$overVoltage)))]
    set synchPhaseDeg [expr 180-180.0*$synchPhase/$pi]
    set synchTune [expr $alphac*$harmonic*cos($synchPhase)*$voltage/(2*$pi*$energy)]
    set omegaRF [expr $harmonic*2*$pi*$revFreq]
    set rfFrequency [expr $omegaRF/(2*$pi*1.0e6)]
    set bunchLength [expr $Sdelta0*sqrt(2*$pi*$alphac*$harmonic*$cMKS*$cMKS*$energy/($omegaRF*$omegaRF*cos($synchPhase)*$voltage))]
    update
}

compute
APSButton .compute -parent .userFrame -text Compute -command compute

